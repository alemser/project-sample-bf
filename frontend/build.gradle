buildscript {
    repositories {
        mavenCentral()
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }

    dependencies {
        classpath 'com.moowork.gradle:gradle-node-plugin:1.3.1'
    }
}

apply plugin: 'base'
apply plugin: 'com.moowork.node'

node {
    version = '10.15.3'
    npmVersion = '6.4.1'
    download = false
}

npm_run_build {
    inputs.files fileTree('public')
    inputs.files fileTree('src')

    inputs.file 'package.json'
    inputs.file 'package-lock.json'

    outputs.dir 'build'
}

task packageFrontend(type: Zip) {
    dependsOn npm_run_build
    baseName 'frontend'
    extension 'jar'
    destinationDir file("${projectDir}/build_packageFrontend")
    from('build') {
        into 'static'
    }
}

configurations {
    npmResources
}

configurations.default.extendsFrom(configurations.npmResources)

artifacts {
    npmResources(packageFrontend.archivePath) {
        builtBy packageFrontend
        type 'jar'
    }
}

assemble.dependsOn packageFrontend

String testsExecutedMarkerName = "${projectDir}/.tests.executed"

task test(type: NpmTask) {
    dependsOn assemble

    environment CI: 'true'

    args = ['run', 'test']
    
    inputs.files fileTree('src')
    inputs.file 'package.json'
    inputs.file 'package-lock.json'

    doLast {
        new File(testsExecutedMarkerName).text = 'delete this file to force re-execution JavaScript tests'
    }
    outputs.file testsExecutedMarkerName
}

check.dependsOn test

clean {
    delete packageFrontend.archivePath
    delete testsExecutedMarkerName
}
